plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.org"
		content {
			includeGroupByRegex("org.parchmentmc.*")
		}
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
		content {
			includeGroupByRegex("com.terraformersmc.*")
		}
	}
	maven {
		name = "Shedaniel Maven"
		url = "https://maven.shedaniel.me/"
		content {
			includeGroupByRegex("me.shedaniel.*")
		}
	}
	maven {
		name = "Jitpack"
		url = "https://jitpack.io"
		content {
			includeGroupByRegex("com.gitlab.Lortseam.*")
		}
	}
	maven {
		name = "Siphalor" // Coat, dependency for complete conifg
		url = "https://maven.siphalor.de"
		content {
			includeGroup("de.siphalor")
		}
	}
	maven {
		name = "Architectury"
		url = "https://maven.architectury.dev/"
		content {
			includeGroup("dev.architectury")
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${project.parchment_version}@zip")
	}
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

	// 04/06/2022: crash with bundled architectury version
	// TODO: remove exclude stuff
	modImplementation("dev.architectury:architectury-fabric:${project.rei_architectury_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}") {
		exclude(group: "dev.architectury", module: "architectury-fabric")
		exclude(group: "net.fabricmc.fabric-api")
	}
	modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
		exclude(group: "dev.architectury", module: "architectury-fabric")
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
